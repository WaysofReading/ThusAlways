label day4_morning:
    'DAY 4 - MORNING'
    call hero_paths_choice
    return

label day4_combat:
    ## NEW SCENE: Day 4 Combat
    ## FADE IN to CABIN EXTERIOR. FADE IN Outside sfx
    scene cabin_exterior with fade
    play music foley_desert_wind fadein 1.0
    guide "“Again, Gavin. You swing too close to yourself. You’ll need more leverage and a broader reach to maximize the damage you can cause.”"

    ## FADE IN Gavin
    ## SPRITE Gavin A
    show gavin annoyed at grouped_left_gavin
    with dissolve
    gavin "“Yes, Guide.”"
    guide "“Morgan, concentrate on your surroundings. Your duty is to care for your well-being, and that of your friends. What’s immediately in front of you may not always be the most important thing.”"
    
    ## FADE IN Morgan
    ## SPRITE Morgan N
    show morgan at grouped_center_lance
    with dissolve
    morgan "“Okay.”"
    guide "“Lance... for the love of the gods, {i}please{/i} relax your shoulders.”"
    
    ## FADE IN Lance sprite
    ## SPRITE Lance N
    show lance at grouped_right_morgan
    with dissolve
    lance "“I’m {i}trying{/i}.”"
    guide_dark "“Try --”"
    guide "“-- Try exhaling when you cast, instead of inhaling.”"
    guide_dark "You can’t interrupt me!"
    guide "I just did."
    guide_dark "You and they are taking {i}too long{/i}."
    guide_dark "What next? You’ll take them on a journey to find ice cream?"
    guide_dark "They’ll have to leave the nest sooner rather than later."
    guide_dark "Don’t tell me you’ve gone {i}soft{/i}."
    guide "{cps=30}...{/cps}"
    guide_dark "Oh, you can’t be serious."
    guide "This isn’t right. Or fair."
    guide_dark "Right? Fair? When has that ever stopped any of this?"
    guide_dark "The cycle doesn’t {i}care{/i} about your precious right or fair."
    guide_dark "You only get what you’ve {i}earned{/i}. And if nothing changes..."
    
    ## SPRITES Gavin UNC, Morgan SP, Lance ANG
    ## There’s a ROAR of a corrupted monster somewhere closeby.
    play sound monster_roar
    pause 1.5
    show gavin uncomfortable
    show morgan surprised
    show lance angry
    pause 0.5
    guide_dark "... That means you didn’t {i}earn{/i} any change."
    play music bgm_find_the_master fadein 1.0

    ## The HEROES draw their weapons.
    ## SPRITE Morgan SP
    show morgan surprised
    morgan "“They followed us here..?”"

    ## SPRITE Gavin A
    show gavin annoyed
    gavin "“I guess it had to happen sooner or later. We’ve got this!”"

    ## BEGIN COMBAT
    call combat_encounter_corrupted_strong_day4_combat
    
    ## AFTER COMBAT
    ## SPRITES Gavin A, Morgan A, Lance N
    scene cabin_exterior with fade
    play music foley_desert_wind fadein 1.0
    show gavin annoyed at grouped_center_gavin
    show lance annoyed at grouped_center_lance
    show morgan annoyed at grouped_center_morgan
    with dissolve
    call restore_from_combat
    guide_dark "“Good. That won’t be the last of them though.”"
    guide "You! Stop talking, you’re going to --"
    guide_dark "Relax, I’ll remember to breathe this time."

    ## SPRITE Gavin SD
    show gavin sad
    gavin "“Yeah... usually once they start showing up, they don’t stop.”"

    ## SPRITE Lance A
    show lance annoyed
    lance "“So it’s not safe here anymore?”"
    guide_dark "“You’ll be fine. Besides, it will be good practice.”"
    guide "You’re contradicting yourself! The other day you said we needed to --"
    morgan "“I thought you said we were going to avoid ending up in situations that could be dangerous?”"
    guide_dark "“We’re going to avoid {i}intentionally{/i} seeking out such dangers, like you did. If they’re going to come, we may as well make the most of it.”"
    lance "“But more will come, right? What will we do when there are too many?”"
    guide_dark "“{i}Then{/i} we can depart. But not a moment sooner.”"
    guide_dark "“Your training is nearing its culmination. We can’t get distracted now.”"
    
    ## The HEROES all look at each other.
    ## SPRITES Morgan N, Lance N
    show morgan neutral
    show lance neutral
    lance "“... Okay... if you say so.”"
    pause 1.0

    ## SPRITE Gavin A
    show gavin annoyed with dissolve
    gavin "“No.”"
    lance "“No?”"
    gavin "“Something isn’t adding up.”"
    gavin "“We came here to be trained by you, and you tried to ignore us so we would leave. When that didn’t work, you tried to discourage us.”"
    gavin "“Then when {i}that{/i} didn’t work, you finally gave up trying to get us to leave, but you were really picky about what you told us, and how.”"
    gavin "“Now you’re suddenly... {i}eager{/i} for us to get stronger and to fight these things.”"
    
    ## SPRITE Morgan SD
    show morgan sad
    morgan "“It’s my fault, isn’t it?”"
    gavin "“It’s not, and you {i}know{/i} it’s not.”"
    
    ## SPRITE Gavin SD
    show gavin sad
    gavin "“Guide, I will fight with every ounce of strength in me to see all of this through, but there’s something {i}big{/i} you’re not telling us.”"
    gavin "“And I feel like we’re going to be in trouble if you don’t tell us what it is.”"
    gavin "“So please, be honest with us.”"
    gavin "“What is {i}happening?{/i}”"
    pause 0.75

    guide_dark "Whoops."
    guide "You {i}impatient fool!{/i}"
    guide_dark "No, no, we can make this work."
    guide "{i}We?{/i}"
    guide "{i}I{/i} had nothing to do with this. You dug this hole yourself."
    guide_dark "Mmm, no, you handle this. Goodbye."
    
    ## Play GASP sound effect to indicate return of control to the GUIDE
    play sound gasp_man volume 0.33

    ## CHOICE:
    ## >Tell them everything.
    ## >Lie.
    ## >Say nothing.
    ## (“Tell them everything” and “Lie” trees both lead to the same tree. Choices merge at > RECONVENE)

    menu:
        "Tell them everything":
            jump day4_combat_choice_both
        "Lie":
            jump day4_combat_choice_both

    ## >Tell them everything.
    ## >Lie
    label day4_combat_choice_both:
        guide "“{cps=30}...{/cps}”"
        guide_dark "{cps=30}...{/cps}"
        guide "You asshole. You {i}know{/i} I can’t lie, nor can I give them the whole truth."
        guide "You’ve left me with an {i}impossible{/i} choice."
        guide "Though I suppose my choices haven’t mattered much, thus far."
        guide "No matter what I do, it’s not enough for you. You’re too impatient. There’s no way to win."
        guide "There never {i}has{/i} been."
        guide "And this time will be no different."
        ## >Say nothing.
        guide "We both know there’s no point in me trying to say anything. You won’t allow it."
        guide "Was this all just to mock me? To mock me and my efforts, even though there’s {i}nothing{/i} I could have done to make this go any differently?"
        guide "You hold the cards. You have since the start. I’m just another one of your puppets unable to cut loose from its strings."
        guide "Despite my best efforts... nothing ever changes."
        jump day4_combat_choice_reconvene
    
    ## > RECONVENE
    label day4_combat_choice_reconvene:
        ## SPRITE Lance SD
        show lance sad
        lance "“... Come on. {i}Say{/i} something.”"
        guide "“... I... can’t.”"
        guide "“It wouldn’t change anything even if I could.”"

        ## SPRITE Lance A
        show lance annoyed
        lance "“Even if you could..?”"

        ## SPRITE Gavin ANG
        show gavin angry
        gavin "“Come on! You think we believe that?”"
        pause 0.75

        ## SPRITE Lance N
        show lance neutral with dissolve
        lance "“I do.”"

        ## SPRITE Gavin SP
        show gavin surprised
        gavin "“Lance?”"
        lance "“We’re Chosen, remember? Prophesied heroes.”"
        lance "“And the Guide? It’s as the Roots said.”"
        lance "“He was Chosen once, too.”"
        lance "“Whatever it is he’s not telling us, it’s probably for good reason.”"
        lance "“Though, if possible, I {i}do{/i} want to know one thing.”"
        lance "“Are we going to live?”"

        ## SPRITE Gavin A
        show gavin annoyed
        guide "Of all the questions I could answer..."
        guide_dark "“It’s very likely.”"
        guide_dark "“You have fought hard and trained harder.”"
        guide_dark "“You are becoming the best versions of yourselves.”"
        guide_dark "“Stronger. Faster. More confident. More {i}powerful{/i}.”"
        guide_dark "“Such power is enviable.”"
        guide_dark "“It will be a great catalyst for the future.”"
        pause 1.0

        ## SPRITE Gavin SD
        show gavin sad
        gavin "“I still have so many questions. So many doubts.”"
        gavin "“But I have to trust you. What other choice is there?”"
        guide "What other choice, indeed..."

        ## SPRITE Lance A
        show lance annoyed
        lance "“Fine then. Let those monsters come.”"
        lance "“The more of them we kill, the more we’ll understand how to sense them from afar.”"
        lance "“Then, eventually... the Hidden Darkness itself.”"

        ## SPRITE Morgan H
        show morgan happy
        morgan "“... Maybe we’ll be okay, then.”"
        guide_dark "“Hold on tight to your hope.”"
        guide_dark "“Let it make you stronger.”"
        guide_dark "“Soon, the end will be upon us. And you will be ready.”"

    ## FADE TO BLACK
    scene backdrop_black with dissolve
    stop music fadeout 1.0
    stop music_extra fadeout 1.0
    stop sound fadeout 1.0
    stop sound_extra fadeout 1.0
    guide "{cps=20}... Forgive me.{/cps}"
    call end_scene_fade_to_black
    
    ## END SCENE
    return

label day4_dream:
    ## NEW SCENE: Night 4 Dream
    ## FADE IN to MISTY BG.
    ## DRONING ambiance sfx. Something that sounds science lab-like.
    scene backdrop_mist with fade
    stop music fadeout 1.0
    play music foley_lab_ambiance fadein 1.0 volume 0.5
    lucas "“... This can’t be right. Can it?”"
    lucas "“This isn’t some... mystical cave or secret fountain entrance.”"
    lucas "“It’s just some... {i}lab{/i}.”"
    lucas "“There’s hardly anything ‘hidden’ about this. Besides, the Darkness wasn’t man-made, either, so why would it be here?”"
    gwen "“This is where the aura has been leading us. There’s obviously {i}something{/i} here that’s important and related to the Darkness.”"
    gwen "“Even if it’s not the Hidden Darkness itself.”"
    gwen "“If there’s nothing here, we can turn around and leave once we’re sure.”"
    lucas "“I don’t {i}want{/i} to be ‘pretty sure.’ I want to {i}know{/i}.”"
    lucas "“I’m {i}tired{/i}, Gwen.”"
    gwen "“I know, Lucas. Just a little further.”"

    ## A pause for FOOTSTEPS
    play sound footsteps_echoing
    pause 1.0
    guide "“... How do we know it isn’t man-made?”"
    gwen "“What?”"
    guide "“The Hidden Darkness. Lucas said it wasn’t man-made. We don’t know that.”"
    lucas "“It’s -- it’s not very likely, at least. ‘Highly improbable,’ my dad would say.”"
    guide "“They made nukes. Why wouldn’t they make something else awful?”"
    gwen "“Hush. Wondering where the darkness came from isn’t going to do us any good now.”"
    gwen "“We just need to defeat it and make sure it can’t hurt anyone else.”"
    gwen "“Right, General Mordred?”"
    mordred "“Don’t you kids look at me. I’m just here to get you through one locked door to the next.”"
    mordred "“If you children are going to be the ones to save us from this insanity, I’ll take you to every last corner of the earth if needs must.”"
    guide "“So we do this, and then the Darkness goes away.”"
    guide "“It’s {i}finally{/i} almost over.”"
    guide "{i}Screw{/i} you."
    guide "You {i}would{/i} show this to me. You {i}would{/i} make a big deal out of this moment in particular."
    gwen "“So... what are we gonna do? I mean, when this is all over?”"
    gwen "“... None of us have homes to go back to.”"
    lucas "“I don’t care. I still want to go home.”"
    lucas "“I want my house. My room. My bed.”"
    lucas "“Even... even if it’s just me there.”"
    gwen "“I mean, there has to be {i}someone{/i} out there we know still alive. At {i}least{/i} one person.”"
    gwen "“Maybe my big brother made it. He’d take care of us.”"
    guide "“You guys really think there’s still anything left to save?”"
    gwen "“Don’t be negative. There’s gotta be {i}something{/i}.”"
    guide "“There’s dirt. And burnt things.”"
    guide "“What matters most right now is we banish the Darkness.”"
    guide "“Then we can see what survived and what we can fix.”"
    guide "Gods, I was naive."
    guide "I really thought that. I really thought we could just banish the Darkness and call it mission complete."
    guide "I really thought it was so simple, that it could all end by changing just {i}one{/i} thing in the world."
    guide "I never considered how changing the world would change {i}me{/i}."

    ## Sound of a KEYCARD and a MECHANICAL DOOR sliding open. Something POWERS ON (no idea what this would sound like at the moment)
    play sound keycard_swipe
    pause 1.0
    play sound science_door
    pause 2.0
    play sound light_switch
    pause 1.0
    play music_extra foley_electric_hum volume 0.4
    lucas "“What -- oh -- oh, god --”"
    gwen "“What the -- it’s a bloodbath in here! Get your weapons --”"
    mordred "“I assure you, this happened a long time ago. Janitors are in short supply these days, unfortunately.”"
    gwen "“... If you’re sure...”"
    gwen "“This blood is pretty fresh, though.”"
    mordred "“Keenly observed, young lady.”"
    mordred "“That doesn’t mean the danger that caused this hasn’t passed.”"
    pause 1.5
    
    lucas "“Wait... that’s the thing! The thing they were talking about on the radio!”"
    lucas "“The machine they were gonna try to stop the Corruption with!”"
    lucas "“And -- and it looks like it’s complete!”"
    lucas "“But it’s so {i}huge{/i}. We can’t move this thing out of here.”"
    gwen "“If it works -- we won’t have to fight anymore!”"
    gwen "“General, did -- do you know if it’s been tested?”"
    guide "“Guys --”"
    mordred "“I know a little about this project. The device can only be activated once. And it requires an {i}immense{/i} amount of power to run.”"
    mordred "“It could be an amount of power we don’t have the capability to produce anymore.”"
    gwen "“But -- we have to {i}try{/i}.”"
    guide "“Guys --!”"
    lucas "“Maybe the Darkness came and killed these scientists... Because they were too close to turning it on?”"
    lucas "“But... if the Darkness is {i}gone{/i}...”"
    guide "“There are {i}dead people{/i} here. Dead. People.”"
    guide "“Not Corrupted dead. {i}Dead{/i} dead.”"
    mordred "“... And?”"
    mordred "“I’m not sure what you’re trying to say here, kid. Lucas already made that point.”"
    guide "“Lucas, go look at one of the bodies.”"
    lucas "“I -- what? Me?”"
    guide "“Just {i}do it{/i}.”"

    ## FOOTSTEPS sfx
    play sound footsteps_concrete
    pause 3.5
    lucas "“... Oh...”"
    guide "“Yeah. ‘Oh’. Say it out loud, Lucas.”"
    lucas "“... It... I-It’s...”"
    lucas "“... It’s a bullet hole.”"

    ## FADE OUT droning ambiance
    ## FADE IN sinister music
    stop music fadeout 1.0
    play music bgm_dangerous_path fadein 1.0
    guide "Gods, you were such a bastard. Leading us on like that."
    guide "Such a bastard for making it so {i}obvious{/i} from the start, like this is all some joke to you."
    guide "You always did like to pretend you were so helpful while you were pulling the wool over our eyes."
    guide "And I’ve never figured out what exactly it is you get out of this."
    guide "How do you benefit from breaking everything down, just to build it back up again?"
    guide "How do you benefit from making everything so godsdamned {i}miserable{/i} with your endless quest for power?"
    guide "You’ll never be satisfied. You’ll never have enough, and you’ll keep consuming everything until you don’t even have your own fingers to gnaw on."
    guide "Because there always has to be {i}more{/i}, right?"
    guide "Just get this over with. Say it. Say the riddle."
    mordred "“{b}{cps=30}Close your eyes, and there I am.\nI can be heavy, yet I weigh not a gram.{/cps}{/b}”"
    mordred "“{b}{cps=30}When I’m not repelled, I’m all you can see.\nMake sure I’m around, and all sense will be free.{/cps}{/b}”"
    gwen "“Oh, shi --”"

    ## The MUSIC stops and the MISTY BG disappears immediately.
    $ transition_hold_seconds = 3.0
    call end_scene_fade_to_black_instant_pause
    
    ## END SCENE
    return
